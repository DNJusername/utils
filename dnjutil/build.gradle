apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//定义两个链接
def gitUrl = 'https://github.com/DNJusername/utils.git'//git仓库
def siteUrl = 'https://github.com/DNJusername/utils'//项目主页
version = "0.1"              // 数据仓库依赖第三部分
android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
//    sourceSets {
//        main {
//            res.srcDirs =
//                    [
//                            'src/main/res/layouts/dialog',
//                            'src/main/res'
//                    ]
//        }
//    }
}

install{
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'dnj utils'     // 项目名称
                url siteUrl
                //软件开源协议,现在一般都是Apache License2.0
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dnjID'                // 开发者的id
                        name 'DuanNingJian'              // 开发者名字
                        email 'dnjusername@gmail.com'    // 开发者邮箱
                    }
                }
                scm {
                    connection gitUrl //Git仓库地址
                    developerConnection gitUrl //Git仓库地址
                    url siteUrl //项目主页
                }
            }
        }
    }
}

//生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成jarDoc的task,不需要修改
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false //忽略注释语法错误，如果用jdk1.8，注释写的不规范就不能编译
}

//生成javaDoc的jar，不需要修改
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
//这里是读取Bintray相关的信息，上传项目到GitHub上时会把gradle文件传上去，所以不要把账号密码的信息直接写在这里，而是写在local.properties中，这里动态读取
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") //bintray的用户名
    key = properties.getProperty("bintray.apikey") //bintray刚保存的ApiKey
    configurations = ['archives']
    pkg {
        repo = "utils"       //repository的名字
        name = "dnj.utils"             // 数据仓库依赖第二部分
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true //是否是公开项目
    }
}
//指定编译的编码
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:recyclerview-v7:27.1.1'

}
